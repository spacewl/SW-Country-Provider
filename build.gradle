buildscript {
    File secretPropsFile = project.rootProject.file("local.properties")
    if (secretPropsFile.exists()) {
        Properties properties = new Properties()
        new FileInputStream(secretPropsFile).withCloseable { is -> properties.load(is) }
        properties.each { name, value -> ext[name] = value }
    } else {
        ext["ossrhUsername"] = ""
        ext["ossrhPassword"] = ""
        ext["sonatypeStagingProfileId"] = ""
        ext["signing.keyId"] = ""
        ext["signing.password"] = ""
        ext["signing.key"] = ""
    }

    ext {
        PUBLISH_GROUP_ID = "com.spacewl"
        PUBLISH_VERSION = "1.0.0"
        PUBLISH_ARTIFACT_ID = "countryprovider"
    }
}

plugins {
    id "com.android.library" version "7.1.1" apply false
    id "org.jetbrains.kotlin.android" version "1.6.10" apply false
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0" apply true
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

nexusPublishing {
    repositories {
        sonatype {
            stagingProfileId = sonatypeStagingProfileId
            username = ossrhUsername
            password = ossrhPassword
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}